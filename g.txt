%token # Ident IntConst FloatConst 'const' 'int' 'float' '[' ']' '=' '{' '}' ',' ';' 'void' '(' ')' 'if' 'else' 'while' 'break' 'continue' 'return' '+' '−' '!' '*' '/' '%' '<' '>' '<=' '>=' '==' '!=' '&&' '||' ε
{
CompUnit':CompUnit
CompUnit:DeclOrFuncDef CompUnitList
CompUnitList:DeclOrFuncDef CompUnitList#|#ε
DeclOrFuncDef:Decl#|#FuncDef
Decl:ConstDecl#|#VarDecl
ConstDecl:'const' BType ConstDef ConstDefList ';'
BType:'int'#|#'float'
ConstDef:Ident ConstExpList '=' ConstInitVal
ConstDefList:',' ConstDef ConstDefList#|#ε
ConstExpList:'[' ConstExp ']' ConstExpList#|#ε
ConstInitVal:ConstExp#|#'{' OptionalConstInitValList '}'
OptionalConstInitValList:ConstInitVal ConstInitValList#|#ε
ConstInitValList:',' ConstInitVal ConstInitValList#|#ε
VarDecl:BType VarDef VarDefList ';'
VarDef:VarDefNoEqual#|#VarDefWithEqual
VarDefNoEqual:Ident VarDefNoEqualList
VarDefWithEqual:Ident VarDefWithEqualList '=' InitVal
VarDefNoEqualList:'[' ConstExp ']' VarDefNoEqualList#|#ε
VarDefWithEqualList:'[' ConstExp ']' VarDefWithEqualList#|#ε
VarDefList:',' VarDef VarDefList#|#ε
InitVal:Exp#|#'{' OptionalInitValList '}'
OptionalInitValList:InitVal InitValList#|#ε
InitValList:',' InitVal InitValList#|#ε
FuncDef:FuncType Ident '(' OptionalFuncFParams ')' Block
OptionalFuncFParams:FuncFParams#|#ε
FuncType:'void'#|#'int'#|#'float'
FuncFParams:FuncFParam FuncFParamList
FuncFParamList:',' FuncFParam FuncFParamList#|#ε
FuncFParam:BType Ident OptionalBracketExpList
OptionalBracketExpList:'[' ']' BracketExpList#|#ε
BracketExpList:'[' Exp ']' BracketExpList#|#ε
Block:'{' BlockItemList '}'
BlockItemList:BlockItem BlockItemList#|#ε
BlockItem:Decl#|#Stmt
Stmt:LVal '=' Exp ';'#|#OptionalExp ';'#|#Block#|#'if' '(' Cond ')' Stmt OptionalElse#|#'while' '(' Cond ')' Stmt#|#'break' ';'#|#'continue' ';'#|#'return' OptionalExp ';'
OptionalExp:Exp#|#ε
OptionalElse:'else' Stmt#|#ε
Exp:AddExp
Cond:LOrExp
LVal:Ident ExpList
ExpList:'[' Exp ']' ExpList#|#ε
PrimaryExp:'(' Exp ')'#|#LVal#|#Number
Number:IntConst#|#FloatConst
UnaryExp:PrimaryExp#|#Ident '(' OptionalFuncRParams ')'#|#UnaryOp UnaryExp
OptionalFuncRParams:FuncRParams#|#ε
UnaryOp:'+'#|#'−'#|#'!'
FuncRParams:Exp ExpListTail
ExpListTail:',' Exp ExpListTail#|#ε
MulExp:UnaryExp MulExp'
MulExp':MulExpOp UnaryExp MulExp'#|#ε
MulExpOp:'*'#|#'/'#|#'%'
AddExp:MulExp AddExp'
AddExp':AddExpOp MulExp AddExp'#|#ε
AddExpOp:'+'#|#'−'
RelExp:AddExp RelExp'
RelExp':RelExpOp AddExp RelExp'#|#ε
RelExpOp:'<'#|#'>'#|#'<='#|#'>='
EqExp:RelExp EqExp'
EqExp':EqExpOp RelExp EqExp'#|#ε
EqExpOp:'=='#|#'!='
LAndExp:EqExp LAndExp'
LAndExp':'&&' EqExp LAndExp'#|#ε
LOrExp:LAndExp LOrExp'
LOrExp':'||' LAndExp LOrExp'#|#ε
ConstExp:AddExp
}